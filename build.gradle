apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'eclipse'

group = 'org.gradle.plugins'
version = '1.0-SNAPSHOT'

sourceCompatibility = 1.5
targetCompatibility = 1.5

ideaModule {
  excludeDirs += file('buildSrc/build')
  excludeDirs += file('buildSrc/.gradle')
}

ideaProject {
  javaVersion = '1.6'
}

configurations {
  deployerJars
  all*.exclude module: 'google-collections' //we use guava instead
}

task sourcesJar(type: Jar, dependsOn: compileJava) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

artifacts {
  archives sourcesJar
}

//If USER and PASSWORD are set on the project model via the CLI, set up an archive upload destination
// Command line specification would look like: gradle -PUSER=matthew -PPASSWORD=foobar 
if (project.hasProperty("USER")) {
  println "Setting up the upload (destination) repository for this plugin"
  uploadArchives {
    repositories.mavenDeployer {
      configuration = configurations.deployerJars
      pom.artifactId = 'gradle-m2metadata-plugin'
      snapshotRepository(url: "http://repo.jfrog.org/artifactory/gradle-snapshots-local") {
        authentication(userName: USER, password: PASSWORD)
      }
    }
  }
}

//uploadArchives {
//    repositories {
//        ivy {
//            url "file://$buildDir/sharedRepo"
//        }
//    }
//}

repositories {
    mavenCentral()
}

dependencies {
  //Maven
  compile "org.apache.maven:maven-core:3.0-beta-1"
  compile "org.apache.maven:maven-model:3.0-beta-1"
  compile "org.apache.maven:maven-artifact:3.0-beta-1"
  compile "org.apache.maven:maven-model-builder:3.0-beta-1"
  compile "org.apache.maven:maven-compat:3.0-beta-1"
  compile "org.codehaus.plexus:plexus-container-default:1.5.5"
  compile "org.codehaus.plexus:plexus-classworlds:2.2.3"

  //Maven plugins
  compile "org.apache.maven.plugins:maven-clean-plugin:2.4.1"

  //Maven wagons
  compile "org.apache.maven.wagon:wagon-http:1.0-beta-6"
  compile "org.apache.maven.wagon:wagon-ssh:1.0-beta-6"
  compile "org.apache.maven.wagon:wagon-ssh-external:1.0-beta-6"
  compile "org.apache.maven.wagon:wagon-file:1.0-beta-6"
  compile "org.apache.maven.wagon:wagon-webdav-jackrabbit:1.0-beta-6"
  compile "org.apache.maven.wagon:wagon-http-lightweight:1.0-beta-6"

  //Utils
  compile "com.google.guava:guava:r07"

  //Gradle Core APIs with a specific exclusion of maven-ant-tasks because it (older)
  // provides DefaultPlexusContainer and conflicts with org.codehaus.plexus:plexus-container-default (newer)
  compile gradleApi().source.filter { !it.name.startsWith("maven-ant-tasks") }
}

task wrapper(type: Wrapper)